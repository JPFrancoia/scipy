"""Cython wrappers for root finders in c_zeros"""

{{default root_finders = ['bisect', 'ridder', 'brenth', 'brentq']}}

from . cimport c_zeros


# callback function wrapper that extracts function, args from params struct
cdef double scipy_zeros_functions_func(double x, void *params):
    cdef zeros_parameters *myparams = <zeros_parameters *> params
    cdef void* args = myparams.args
    cdef callback_type f = myparams.function
    # callback_type takes a double and a struct
    return f(x, args)


{{for ROOT_FINDER in root_finders}}
# cythonized way to call scalar {{ROOT_FINDER}}
cdef double {{ROOT_FINDER}}(callback_type f, double xa, double xb, void *args,
                            double xtol, double rtol, int iter,
                            zeros_parameters *full_output):
    cdef zeros_parameters myparams
    # create params struct
    myparams.args = args
    myparams.function = f
    myparams.root = c_zeros.{{ROOT_FINDER}}(
        scipy_zeros_functions_func, xa, xb, xtol, rtol, iter,
        <c_zeros.default_parameters *> &myparams)
    if full_output is not NULL:
        full_output = &myparams
    return myparams.root


#
{{endfor}}

# test examples

ARGS = (0.0, 0.0, 1.0)
A0 = tuple(-2.0 - x/10.0 for x in range(10))
XLO, XHI = 0.0, 2.0
XTOL, RTOL, MITR = 0.001, 0.001, 10


# extra parameters
ctypedef struct extra_params:
    double[4] a


# full output structure
ctypedef struct full_output_struct:
    int funcalls
    int iterations
    int error_num
    double root


# callback function
cdef double f_example(double x, void *args):
    cdef extra_params *myargs = <extra_params *> args
    cdef double[4] a
    # unpack structure
    a = myargs.a
    return a[3]*x**3 + a[2]*x**2 + a[1]*x + a[0]


{{for ROOT_FINDER in root_finders}}
# {{ROOT_FINDER}} example
cdef double {{ROOT_FINDER}}_example(tuple args):
    cdef extra_params myargs
    myargs.a = args
    return {{ROOT_FINDER}}(
        f_example, XLO, XHI, &myargs, XTOL, RTOL, MITR, NULL)


#
{{endfor}}

# map example functions
EXAMPLES_MAP = {
    {{for ROOT_FINDER in root_finders}}
    '{{ROOT_FINDER}}': {{ROOT_FINDER}}_example,
    {{endfor}}
}

# python function
def loop_example(method, a0=A0, args=ARGS):
    """example of zeros functions in a loop"""
    method = EXAMPLES_MAP[method.lower()]
    args = [(a0_,) + args for a0_ in a0]
    return map(method, args)


# brentq example with full ouptut
cdef full_output_struct brentq_full_output_example(tuple args):
    cdef full_output_struct my_full_output
    cdef zeros_parameters full_output
    cdef extra_params myargs
    myargs.a = args
    my_full_output.root = brentq(
        f_example, XLO, XHI, &myargs, XTOL, RTOL, MITR, &full_output)
    my_full_output.funcalls = full_output.funcalls
    my_full_output.iterations = full_output.iterations
    my_full_output.error_num = full_output.error_num
    return my_full_output


# python function
def full_output_example():
    return brentq_full_output_example((A0[0],) + ARGS)
